[@Marrage]
{
#Say
<Img|x=0.0|y=0.0|height=222|bg=1|Move=0|img=public/bg_npc_01.png|reset=1|show=0>
<Button|x=386|y=10|nimg=public/1900000510.png|pimg=public/1900000511.png|link=@Exit>
<Layout|x=385|y=10|width=80|height=80|link=@Exit>
<RText|x=26.0|y=20.0|color=255|size=18|text=欢迎你来婚姻圣殿，你来这里一定有你想做的\>
<Text|x=26.0|y=55.0|color=255|size=18|text=我能帮你些什么呢？\>

<Text|x=50.0|y=100.0|color=251|size=18|text=我要结婚|link=@请求结婚>
<Text|x=180.0|y=100.0|color=251|size=18|text=和平离婚|link=@和平离婚>
<Text|x=300.0|y=100.0|color=251|size=18|text=强制离婚|link=@强制离婚>
<Text|x=50.0|y=134.0|color=251|size=18|text=指定婚戒|link=@指定婚戒>
<Text|x=180.0|y=134.0|color=251|size=18|text=摘下婚戒|link=@摘下婚戒>
<Text|x=300.0|y=134.0|color=251|size=18|text=重置结婚|link=@重置结婚>


[@重置结婚]
#If
ISADMIN
#ACT
InitMarray
SENDMSG 5 你重置了结婚功能。
#Elsesay
　\\
你不是GM无法操作。
[@请求结婚]
#say
结婚需要双方等级达到40级，费用50万。\\
<我要结婚/@我要结婚>\\
<我不结了/@exit>\

[@我要结婚]
#If
CHECKREQMARRAY
#Act
REQMARRAY
SendCustomMsg 1 新郎:<$USERNAME>向新娘<$MARRY_OTHERNAME>求婚，请宣誓！ 253 255 600 433
Goto @关闭窗口
#Elseact
SendCustomMsg 1 司仪：你都结过婚了！ 253 255 600 433

[@强制离婚]
#say
强制离婚后，将自动解除关系并扣费50万金币，\
确定要强制离婚？\
<强制离婚并支付50万金币/@直接离婚>\\
<我不离了/@exit>\

[@直接离婚]
#If
CHECKREQDIVORCE
#Act
REQDIVORCE 1
SendCustomMsg 1 你已经强制离婚! 253 255 600 433
Goto @关闭窗口

[@和平离婚]
#say
和平离婚需要25万.\

<我要离婚/@我要离婚>\\
<我不离了/@exit>\

[@我要离婚]
#If
CHECKREQDIVORCE
#Act
REQDIVORCE 0
SendCustomMsg 1 对方若不同意或不在线可申请强制离婚 253 255 600 433
Goto @关闭窗口

#Elsesay
你没结婚或金币不足！
[@摘下婚戒]
#say
摘下婚戒后将失去夫妻互飞功能，需要金币50万\
当然我也可以帮你重新戴上新的婚戒\

<确定摘下/@确定摘下>\\
<我不摘了/@exit>\

[@确定摘下]
#If
CHECKTAKEOFFRING
#Act
TAKEOFFRING
SETITEMEFFECT 7 0
SendCustomMsg 1 你的婚戒已经摘下 253 255 600 433
DELAYGOTO 1000 @关闭窗口
#Elsesay
你手上没带婚戒呢,带着婚戒的情况下才能摘下。
[@指定婚戒]
#If
CHECKTAKEONRING
#Say
<ITEMBOX|x=191.0|y=50.0|width=70|height=70|tips=<只能放入戒指/FCOLOR=249>|boxindex=1|stdmode=22,23|tipsx=4|tipsy=100|img=public/1900000651_3.png>
<Button|x=188.0|y=130.0|color=1003|size=16|pimg=public/1900000679_1.png|nimg=public/1900000679.png|text=戴上戒指|link=@戴上戒指>
#Act
OPENHYPERLINK 7
#Elsesay
你没结婚或你手上带着婚戒。
[@戴上戒指]
#Act 
Mov S$婚戒名 <$BOXITEM[1].NAME>
TAKEONRING 1
SETITEMEFFECT 7 4508
SENDMSG 5 你戴上了结婚戒指：<$Str(S$婚戒名)>
SendCustomMsg 1 你戴上了结婚戒指：<$Str(S$婚戒名)> 253 255 600 433 
Goto @关闭窗口

[@MARRAYMSG_1]
#If
#Say
<Text|x=50.0|y=50.0|color=255|size=18|text=<$USERNAME>，你愿意与 <$MARRY_OTHERNAME> 永结同心吗？\>
<Text|x=220.0|y=124.0|color=251|size=18|text=我愿意|link=@同意结婚>
<Text|x=120.0|y=124.0|color=251|size=18|text=我拒绝|link=@拒绝结婚>

[@同意结婚]
#Act
MARRAYAGREE True
Goto @关闭窗口

[@拒绝结婚]
#Act
MARRAYAGREE False
Goto @关闭窗口

[@DIVORCEMSG_1]
#If
#Say
<Text|x=30.0|y=30.0|color=255|size=18|text=<$USERNAME>，<$DIVORCE_OTHERNAME> 想要与您和平离婚，\> 
<Text|x=30.0|y=60.0|color=255|size=18|text=您是否同意？\>
<Text|x=220.0|y=100.0|color=251|size=18|text=我愿意|link=@同意离婚>
<Text|x=120.0|y=100.0|color=251|size=18|text=我拒绝|link=@拒绝离婚>

[@DIVORCEMSG_2]
#If
#Say
<Text|x=30.0|y=30.0|color=255|size=18|text=<$USERNAME>，<$DIVORCE_OTHERNAME> 不在线，\>
<Text|x=30.0|y=60.0|color=253|size=18|text=你可以去强制离婚。\>

[@DIVORCEMSG_3]
#If
#Say
<Text|x=30.0|y=30.0|color=255|size=18|text=<$USERNAME>，<$DIVORCE_OTHERNAME> 不同意和平离婚，\>
<Text|x=30.0|y=60.0|color=253|size=18|text=你可以去强制离婚。\>

[@DIVORCEMSG_4]
#If
#Say
<Text|x=30.0|y=30.0|color=255|size=18|text=<$USERNAME>，<$DIVORCE_OTHERNAME> 已同意和平离婚，\>
<Text|x=30.0|y=60.0|color=255|size=18|text=你是否想花费<$DIVORCE1_NEEDGOLD>金币 和平离婚？\>
<Text|x=220.0|y=100.0|color=251|size=18|text=我愿意|link=@同意离婚>
<Text|x=120.0|y=100.0|color=251|size=18|text=我拒绝|link=@拒绝离婚>

[@同意离婚]
#If
#Act
DIVORCEAGREE True
SendMsg 5 你和<$DIVORCE_OTHERNAME>已经解除婚姻关系。
DELAYGOTO 500 @关闭窗口

[@拒绝离婚]
#If
#Act
DIVORCEAGREE False
DELAYGOTO 500 @关闭窗口

[@关闭窗口]
#Act
CLOSE
SENDMSG 6 关闭窗口

}
